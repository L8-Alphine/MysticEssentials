plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        url "https://maven.nucleoid.xyz/"
        name "Nucleoid"
    }
    mavenCentral()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    modImplementation include("eu.pb4:placeholder-api:2.8.1+1.21.10")

    modCompileOnly "net.luckperms:api:$rootProject.luckperms_api_version"
    implementation "io.lettuce:lettuce-core:6.3.2.RELEASE"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    shadowBundle("io.lettuce:lettuce-core:6.3.2.RELEASE") {
        transitive = false
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

tasks.named("shadowJar", ShadowJar) {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier.set('')
}

//tasks.named("remapJar") {
//    dependsOn tasks.named("shadowJar")
//    inputFile.set(tasks.named("shadowJar", ShadowJar).flatMap { it.archiveFile })
//}
